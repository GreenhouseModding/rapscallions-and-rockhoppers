plugins {
    id 'idea'
    id 'java'
    id 'maven-publish'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}
base {
    archivesName = "${archives_base_name}-common-"
    version = "${version}+${minecraft_version}"
}
minecraft {
    version(minecraft_version)
    if(file("src/main/resources/${mod_id}.accesswidener").exists()){
        accessWideners(file("src/main/resources/${mod_id}.accesswidener"))
    }
}

repositories {
    maven {
        name "SmartBrainLib (SBL) Maven Repo"
        url "https://dl.cloudsmith.io/public/tslat/sbl/maven/"
    }
    maven {
        name "MerchantPug/Greenhouse Maven"
        url 'https://maven.merchantpug.net/releases/'
    }
}

dependencies {
    compileOnly("dev.greenhouseteam.rdpr:rdpr-common-mojmap:${rdpr_version}+${minecraft_version}:api")
    compileOnly("net.tslat.smartbrainlib:SmartBrainLib-common-${minecraft_version}:${sbl_version}")

    annotationProcessor(compileOnly("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))

    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
}

sourceSets {
    named("main") {
        resources.srcDir("src/generated/resources")
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            name = "merchantpug"
            url = "https://maven.merchantpug.net/releases"
            credentials {
                username System.getenv('MAVEN_USERNAME')
                password System.getenv('MAVEN_PASSWORD')
            }
            authentication {
                create("basic", BasicAuthentication)
            }
        }
        mavenLocal()
    }
}