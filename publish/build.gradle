plugins {
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

evaluationDependsOn(":common")
evaluationDependsOn(":fabric")
evaluationDependsOn(":neoforge")

Spec<Task> publishTask = { Task it -> it.name.startsWith("publish") } as Spec<Task>

tasks.matching(publishTask).configureEach {
    it.dependsOn(project(":neoforge").tasks.named("jarJar"))
}

publishMods {
    changelog = rootProject.file("CHANGELOG.md").text
    type = STABLE

    def forgeLoaders = [
            "neoforge"
    ]
    def fabricLoaders = [
            "fabric"
    ]
    def mcVersions = [
            "1.20.4"
    ]

    curseforge("curseforgeFabric") {
        file = project(":fabric").remapJar.archiveFile
        displayName = "v${rootProject.version} (Fabric ${minecraft_version})"
        version = "${rootProject.version}-fabric"

        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "956678"
        modLoaders.addAll(fabricLoaders)
        minecraftVersions.addAll(mcVersions)

        requires {
            slug = "fabric-api"
        }
        embeds {
            slug = "smartbrainlib"
        }
    }

    modrinth("modrinthFabric") {
        file = project(":fabric").remapJar.archiveFile
        displayName = "v${rootProject.version} (Fabric ${minecraft_version})"
        version = "${rootProject.version}-fabric"

        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "9pGITjpO"
        modLoaders.addAll(fabricLoaders)
        minecraftVersions.addAll(mcVersions)

        requires {
            slug = "fabric-api"
        }
        embeds {
            slug = "smartbrainlib"
        }
    }

    curseforge("curseforgeNeoforge") {
        file = project(":neoforge").jar.archiveFile
        displayName = "v${rootProject.version} (NeoForge ${minecraft_version})"
        version = "${rootProject.version}-neoforge"

        accessToken = providers.environmentVariable("CURSEFORGE_TOKEN")
        projectId = "956678"
        modLoaders.addAll(forgeLoaders)
        minecraftVersions.addAll(mcVersions)
        embeds {
            slug = "smartbrainlib"
        }
    }

    modrinth("modrinthNeoforge") {
        file = project(":neoforge").jar.archiveFile
        displayName = "v${rootProject.version} (NeoForge ${minecraft_version})"
        version = "${rootProject.version}-neoforge"

        accessToken = providers.environmentVariable("MODRINTH_TOKEN")
        projectId = "9pGITjpO"
        modLoaders.addAll(forgeLoaders)
        minecraftVersions.addAll(mcVersions)
        embeds {
            slug = "smartbrainlib"
        }
    }

    github {
        displayName = "Rapscallions and Rockhoppers ${rootProject.version} (${minecraft_version})"
        file = project(":fabric").remapJar.archiveFile
        additionalFiles.from(
                project(":neoforge").jar.archiveFile)

        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = "GreenhouseTeam/rapscallions-and-rockhoppers"
        commitish = github_committish // This is the branch the release tag will be created from
        tagName = "${rootProject.version}"
    }
}